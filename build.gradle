plugins {
    id 'xyz.jpenilla.run-paper' version '2.1.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}

shadowJar {
    minimize()
    relocate 'net.kyori', 'net.experience.powered.net.kyori'
    relocate 'org.jetbrains', 'net.experience.powered.org.jetbrains'
}

tasks {
    runServer {
        minecraftVersion("1.20.1")
    }
}

group = 'net.experience.powered'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "spigot-repo"
        url = "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "arim-mvn-lgpl3"
        url = "https://mvn-repo.arim.space/lesser-gpl3/"
    }
}

dependencies {
    implementation("space.arim.morepaperlib:morepaperlib:0.4.3")
    implementation("org.jetbrains:annotations:24.0.0")
    implementation("net.kyori:adventure-api:4.3.0")
    implementation("net.kyori:adventure-platform-bukkit:4.3.0")
    implementation("net.kyori:adventure-text-serializer-legacy:4.13.1")
    implementation("net.kyori:adventure-text-minimessage:4.13.1")
    implementation(project(":API"))
    compileOnly("net.luckperms:api:5.4")
    compileOnly("org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT")
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven {
            name = "spigot-repo"
            url = "https://hub.spigotmc.org/nexus/content/groups/public/"
        }
        maven {
            name = "arim-mvn-lgpl3"
            url = "https://mvn-repo.arim.space/lesser-gpl3/"
        }
    }

    dependencies {
        implementation("space.arim.morepaperlib:morepaperlib:0.4.3")
        implementation("net.kyori:adventure-api:4.3.0")
        implementation("net.kyori:adventure-platform-bukkit:4.3.0")
        implementation("net.kyori:adventure-text-serializer-legacy:4.13.1")
        implementation("org.jetbrains:annotations:24.0.0")
        compileOnly("org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT")
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}