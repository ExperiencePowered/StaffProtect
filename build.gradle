plugins {
    id 'com.palantir.git-version' version '3.0.0'
    id 'xyz.jpenilla.run-paper' version '2.1.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}

shadowJar {
    minimize()
    relocate 'space.arim.morepaperlib', 'net.experience.powered.staffprotect.space.arim.morepaperlib'
}

tasks {
    runServer {
        minecraftVersion("1.20.1")
    }
}

group = 'net.experience.powered'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "spigot-repo"
        url = "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "arim-mvn-lgpl3"
        url = "https://mvn-repo.arim.space/lesser-gpl3/"
    }
}

dependencies {
    implementation("space.arim.morepaperlib:morepaperlib:0.4.3")
    implementation("net.kyori:adventure-api:4.3.0")
    implementation("net.kyori:adventure-platform-bukkit:4.3.0")
    implementation("net.kyori:adventure-text-serializer-legacy:4.13.1")
    implementation("net.kyori:adventure-text-minimessage:4.13.1")
    implementation(project(":API"))
    compileOnly("net.luckperms:api:5.4")
    compileOnly("org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT")
    compileOnly("org.jetbrains:annotations:24.0.0")
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven {
            name = "spigot-repo"
            url = "https://hub.spigotmc.org/nexus/content/groups/public/"
        }
    }

    dependencies {
        implementation("net.kyori:adventure-api:4.3.0")
        implementation("net.kyori:adventure-platform-bukkit:4.3.0")
        implementation("org.jetbrains:annotations:24.0.0")
        compileOnly("org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT")
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.register('createProperties') {
    doLast {
        def details = versionDetails()
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['gitLastTag'] = details.lastTag
            p['gitCommitDistance'] = details.commitDistance.toString()
            p['gitHash'] = details.gitHash.toString()
            p['gitHashFull'] = details.gitHashFull.toString() // full 40-character Git commit hash
            p['gitBranchName'] = details.branchName // is null if the repository in detached HEAD mode
            p['gitIsCleanTag'] = details.isCleanTag.toString()
            p.store w, null
        }
        // copy needed, otherwise the bean VersionController can't load the file at startup when running complete-app tests.
        copy {
            from "$buildDir/resources/main/version.properties"
            into "bin/main/"
        }
    }
}

classes {
    dependsOn createProperties
}